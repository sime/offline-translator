plugins {
  alias(libs.plugins.android.application)
  alias(libs.plugins.kotlin.android)
  alias(libs.plugins.kotlin.compose)
  alias(libs.plugins.kotlin.serialization)
  alias(libs.plugins.ktlint)
  alias(libs.plugins.detekt)
}

val abiCodes = mapOf("armeabi-v7a" to 1, "arm64-v8a" to 2, "x86" to 3, "x86_64" to 4)
val defaultDevAbis = listOf("arm64-v8a", "x86_64")

android {
  namespace = "dev.davidv.translator"
  compileSdk = 34
  ndkVersion = "28.0.12674087"
  buildToolsVersion = "34.0.0"

  sourceSets {
    getByName("main") {
      aidl.srcDir("src/main/aidl")
    }
    getByName("androidTest") {
      assets {
        srcDirs("src/androidTest/assets")
      }
    }
  }
  defaultConfig {
    applicationId = "dev.davidv.translator"
    minSdk = 21
    targetSdk = 34
    versionCode = 11
    versionName = "0.2.6"

    testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
  }

  splits {
    abi {
      isEnable = true
      reset()
      val targetAbi = project.findProperty("targetAbi")?.toString()
      if (targetAbi != null) {
        include(targetAbi)
      } else {
        include(*defaultDevAbis.toTypedArray())
      }
      isUniversalApk = false
    }
  }

  buildTypes {
    release {
      isMinifyEnabled = true
      isShrinkResources = true
      // when building in F-Droid CI, the `cargo` binary is not in path
      // so there's a prebuild step to modify this file and replace "cargo"
      // with the full path to cargo (/home/vagrant/.cargo/bin/..)
      // however, modifying this file leaves the repo in a dirty state
      // which means that the revision in `META-INF/version-control-info.textproto`
      // does not match with the _actual_ commit.
      // Disabling this until I figure out how to put `cargo` in PATH
      // in F-Droid CI
      vcsInfo {
        include = false
      }
      proguardFiles(
        getDefaultProguardFile("proguard-android-optimize.txt"),
        "proguard-rules.pro",
      )
    }
  }
  compileOptions {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
  }
  kotlinOptions {
    jvmTarget = "11"
  }
  buildFeatures {
    aidl = true
    compose = true
  }

  applicationVariants.all {
    outputs.all {
      val output = this as com.android.build.gradle.internal.api.ApkVariantOutputImpl
      val abi = output.getFilter(com.android.build.OutputFile.ABI)
      if (abi != null) {
        output.versionCodeOverride = defaultConfig.versionCode!! * 10 + (abiCodes[abi] ?: 0)
      }
    }
  }
}

val bindingsRootDir = file("src/main/bindings")
val jniLibsRootDir = file("src/main/jniLibs")
val androidSdkRoot =
  System.getenv("ANDROID_SDK_ROOT")
    ?: System.getenv("ANDROID_HOME")
    ?: throw GradleException("ANDROID_SDK_ROOT or ANDROID_HOME must be set")
val ndk = "$androidSdkRoot/ndk/28.0.12674087"
val bindingsAndroidApi = 28
val onnxRuntimeRootDir = file("../third_party/onnxruntime")

fun onnxRuntimeBuildDir(abi: String) = file("${layout.buildDirectory.asFile.get()}/onnxruntime/$abi")

fun onnxRuntimeConfigDir(abi: String) = File(onnxRuntimeBuildDir(abi), "Release")

fun onnxRuntimeSharedLibrary(abi: String) = File(onnxRuntimeConfigDir(abi), "libonnxruntime.so")

fun jniLibAbiDir(abi: String) = File(jniLibsRootDir, abi)

val abiToTaskSuffix =
  mapOf(
    "arm64-v8a" to "Aarch64",
    "armeabi-v7a" to "ArmeabiV7a",
    "x86_64" to "X86_64",
    "x86" to "X86",
  )

val abiToCargoTarget =
  mapOf(
    "arm64-v8a" to "arm64-v8a",
    "armeabi-v7a" to "armeabi-v7a",
    "x86_64" to "x86_64",
    "x86" to "x86",
  )

val prepareOnnxRuntimeSubmodule =
  tasks.register("prepareOnnxRuntimeSubmodule", Exec::class) {
    group = "build"
    description = "Initialize ONNX Runtime submodules"
    workingDir = rootProject.projectDir
    commandLine(
      "git",
      "-C",
      onnxRuntimeRootDir.absolutePath,
      "submodule",
      "update",
      "--init",
      "--recursive",
      "--depth",
      "1",
    )
  }

val abiToOnnxRuntimeTask =
  abiToCargoTarget.keys.associateWith { abi ->
    val taskSuffix = abiToTaskSuffix.getValue(abi)
    val buildTask =
      tasks.register("buildOnnxRuntime$taskSuffix", Exec::class) {
        group = "build"
        description = "Build ONNX Runtime for $abi"
        dependsOn(prepareOnnxRuntimeSubmodule)
        workingDir = onnxRuntimeRootDir
        // The ONNX Runtime checkout includes test fixtures with non-portable Unicode paths.
        // Gradle 8.9 fingerprints Exec task inputs eagerly and fails on CI before the build
        // starts if any input path is unreadable, so let the external build system manage
        // incrementality for this source tree instead.
        doNotTrackState("ONNX Runtime source tree contains CI-unfriendly paths")
        outputs.file(onnxRuntimeSharedLibrary(abi))
        commandLine(
          "python3",
          "tools/ci_build/build.py",
          "--build_dir=${onnxRuntimeBuildDir(abi).absolutePath}",
          "--config=Release",
          "--update",
          "--build",
          "--targets",
          "onnxruntime",
          "--skip_tests",
          "--parallel",
          "--android",
          "--android_abi=$abi",
          "--android_api=$bindingsAndroidApi",
          "--android_sdk_path=$androidSdkRoot",
          "--android_ndk_path=$ndk",
          "--android_cpp_shared",
          "--build_shared_lib",
          "--disable_ml_ops",
          "--disable_generation_ops",
          "--no_kleidiai",
          "--no_sve",
          "--cmake_extra_defines",
          "CMAKE_CXX_STANDARD=20",
          "CMAKE_CXX_STANDARD_REQUIRED=ON",
          "CMAKE_CXX_EXTENSIONS=OFF",
          "--skip_submodule_sync",
        )
      }

    tasks.register("packageOnnxRuntime$taskSuffix", Copy::class) {
      group = "build"
      description = "Copy libonnxruntime.so for $abi into jniLibs"
      dependsOn(buildTask)
      from(onnxRuntimeSharedLibrary(abi))
      into(jniLibAbiDir(abi))
    }
  }

val abiToBindingsTask =
  abiToCargoTarget.mapValues { (abi, cargoTarget) ->
    val taskSuffix = abiToTaskSuffix.getValue(abi)
    tasks.register("buildBindings$taskSuffix") {
      group = "build"
      description = "Build Rust bindings library for $abi"
      dependsOn(abiToOnnxRuntimeTask.getValue(abi))

      doLast {
        exec {
          workingDir = bindingsRootDir
          environment("ANDROID_NDK_ROOT", ndk)
          environment("ANDROID_NDK_HOME", ndk)
          environment("ORT_LIB_LOCATION", onnxRuntimeConfigDir(abi).absolutePath)
          environment("ORT_PREFER_DYNAMIC_LINK", "1")
          commandLine(
            "cargo",
            "ndk",
            "build",
            "--target",
            cargoTarget,
            "--release",
            "--platform",
            bindingsAndroidApi.toString(),
            "--link-libcxx-shared",
            "--output-dir",
            "../jniLibs",
          )
        }
      }
    }
  }

tasks.register("buildOnnxRuntimeAll") {
  group = "build"
  description = "Build ONNX Runtime for all architectures"
  dependsOn(abiToOnnxRuntimeTask.values.toList())
}

tasks.register("buildBindingsAll") {
  group = "build"
  description = "Build Rust bindings library for all architectures"
  dependsOn(abiToBindingsTask.values.toList())
}

val targetAbi = project.findProperty("targetAbi")?.toString()
val bindingsTasks =
  if (targetAbi != null) {
    listOfNotNull(abiToBindingsTask[targetAbi])
  } else {
    defaultDevAbis.mapNotNull { abiToBindingsTask[it] }
  }

tasks.named("preBuild") {
  dependsOn(bindingsTasks)
}

dependencies {

  implementation(libs.androidx.core.ktx)
  implementation(libs.androidx.lifecycle.runtime.ktx)
  implementation(libs.androidx.lifecycle.viewmodel.compose)
  implementation(libs.androidx.activity.compose)
  implementation(platform(libs.androidx.compose.bom))
  implementation(libs.androidx.ui)
  implementation(libs.androidx.ui.graphics)
  implementation(libs.androidx.ui.tooling.preview)
  implementation(libs.androidx.material3)
  implementation(libs.androidx.navigation.compose)
  implementation(libs.androidx.exifinterface)
  implementation(libs.androidx.uiautomator)
  testImplementation(libs.junit)
  androidTestImplementation(libs.androidx.junit)
  androidTestImplementation(libs.androidx.espresso.core)
  androidTestImplementation(platform(libs.androidx.compose.bom))
  androidTestImplementation(libs.androidx.ui.test.junit4)
  debugImplementation(libs.androidx.ui.tooling)
  debugImplementation(libs.androidx.ui.test.manifest)
  implementation(libs.kotlinx.coroutines.android)
  implementation(libs.kotlinx.coroutines.core)
  implementation(libs.androidx.datastore.preferences)
  implementation(libs.kotlinx.serialization.json)
  implementation(project(":app:bergamot"))
  implementation(libs.kotlinx.serialization.json.v162)
  implementation("com.vanniktech:android-image-cropper:4.6.0")
}

ktlint {
  android.set(true)
  ignoreFailures.set(false)
  reporters {
    reporter(org.jlleitschuh.gradle.ktlint.reporter.ReporterType.PLAIN)
    reporter(org.jlleitschuh.gradle.ktlint.reporter.ReporterType.CHECKSTYLE)
  }
  filter {
    exclude { element -> element.file.path.contains("generated/") }
  }
}

detekt {
  toolVersion = "1.23.4"
  config.setFrom(file("$projectDir/detekt-config.yml"))
  buildUponDefaultConfig = true
  allRules = false
}

tasks.register("lintAll") {
  dependsOn("ktlintCheck", "detekt")
  description = "Run all lint checks (ktlint and detekt)"
  group = "verification"
}

tasks.register("formatAll") {
  dependsOn("ktlintFormat")
  description = "Format all code using ktlint"
  group = "formatting"
}
